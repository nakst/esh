#!/usr/bin/env teak

bool debug #option;
bool runTests #option;
bool skipLibraries #option;

void GetOptions() {
	ConsoleWriteStdout("[debug]\nexe=teak\n");
}

void RunTests(str executable) {
	// TODO Timeout.
		
	FileWriteAll("tests_log.txt", "!! running tests...\n");
	int success = 0;
	int total = 0;

	for str file in DirectoryEnumerateRecursively("tests"):assert() {
		bool expected;
		str noRun = "--output-overview" if StringContains(file, ".norun") else "";

		if StringEndsWith(file, ".err.teak") {
			FileAppend("tests_log.txt", "!! expecting error from '%file%'...\n");
			expected = false;
		} else if StringEndsWith(file, ".teak") {
			FileAppend("tests_log.txt", "!! expecting success from '%file%'...\n");
			expected = true;
		} else {
			continue;
		}

		if SystemShellExecute("%executable% --want-completion-confirmation %noRun% tests/%file% 2>> tests_log.txt") == expected
				&& PathDelete("completion_confirmation.txt"):success() { 
			success += 1; 
		} else { 
			FileAppend("tests_log.txt", "!! test failed\n");
			LogError("The test failed.");
		}

		total += 1;
	}

	LogInfo("%success%/%total% tests succeeded.");

	if success < total {
		SystemExit(1);
	}
}

void ProcessBaseModule() {
	str string = FileReadAll("modules/base/index.teak"):assert();
	str result = "";

	for str x in string {
		if x == "\"" { result += "\\\""; }
		else if x == "\\" { result += "\\\\"; }
		else if x == "\n" { result += "\\n\"\n\""; }
		else if x == "\t" { result += "\\t"; }
		else { result += x; }
	}

	FileWriteAll("modules/base/index.h", "/* automatically generated by build.teak, do not modify */\n\"" + result + "\"");
}

void Start() { 
	bool okay;
	str executable;
	str moduleCompilerTemplate;
	str commit = StringSplitByCharacter(StringSplitByCharacter(SystemShellEvaluate("git log"), "\n", false)[0], " ", false)[1];

	ProcessBaseModule();

	if SystemGetHostName() == "Windows" {
		str warningFlags = "/Wall /wd4201 /wd4242 /wd4244 /wd4255 /wd4267 /wd4456 /wd4668 /wd4710 /wd4711 /wd4774 /wd4820 /wd4996 /wd5045";
		str optimizeFlags = "" if debug else "/O2";
		assert SystemShellExecute("cl.exe /DGIT_COMMIT=\\\"%commit%\\\" /Zi teak.c %warningFlags% %optimizeFlags% /link /OUT:new_teak.exe");
		okay = SystemShellExecute("new_teak examples\\hello_world.teak");
		executable = "new_teak";
		moduleCompilerTemplate = "cl.exe /Zi [SRC] %warningFlags% %optimizeFlags% /link /DLL /OUT:[DST].dll";
	} else {
		str optimizeFlags = "-fsanitize=address" if debug else "-O2";
		assert SystemShellExecute("gcc -DGIT_COMMIT=\\\"%commit%\\\" -o new_teak teak.c -g -Wall -Wextra %optimizeFlags% -pthread -ldl"); 
		okay = SystemShellExecute("./new_teak examples/hello_world.teak");
		executable = "./teak";
		moduleCompilerTemplate = "gcc -o l[DST].so -shared [SRC] -fPIC -g -Wall -Wextra %optimizeFlags%";
	}

	if okay {
		if SystemGetHostName() == "Windows" {
			LogInfo("New build looks okay, please run \"move new_teak.exe teak.exe\".");
		} else {
			LogInfo("New build looks okay, replacing existing executable...");
			PathMove("new_teak", "teak");
		}
	} else {
		LogError("New build looks bad.");
		assert false;
	}

	if !skipLibraries {
		for str buildModule in DirectoryEnumerateFiltered("modules", ["*", "build.teak"], -1, true):assert() {
			assert SystemShellExecute("%executable% %buildModule% compilerTemplate=\"%moduleCompilerTemplate%\"");
		}
	}

	if runTests {
		RunTests(executable);
	}
}
